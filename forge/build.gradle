plugins {
	id "me.shedaniel.unified-publishing"
}

loom {
	forge {
		mixinConfigs = [
				"suggestion_tweaker-common.mixins.json",
				"suggestion_tweaker-forge.mixins.json"
		]
	}
}

architectury {
	platformSetupLoomIde()
	forge()
}

configurations {
	common
	shadowCommon
	compileOnly.extendsFrom common
	runtimeClasspath.extendsFrom common
	developmentForge.extendsFrom common
}

dependencies {
	forge "net.minecraftforge:forge:${rootProject.architectury.minecraft}-${rootProject.forge_version}"

	modApi "me.shedaniel.cloth:cloth-config-forge:${rootProject.cloth_version}"

	common(project(path: ":common", configuration: "namedElements")) { transitive false }
	shadowCommon(project(path: ":common", configuration: "transformProductionForge")) { transitive false }
}

processResources {
	inputs.property "version", project.version
	inputs.property "cloth_version", rootProject.cloth_version

	filesMatching("META-INF/mods.toml") {
		expand(
				"version": project.version,
				"cloth_version": rootProject.cloth_version
		)
	}
}

shadowJar {
	configurations = [project.configurations.shadowCommon]
	archiveClassifier.set "dev-shadow"
}

remapJar {
	input.set(shadowJar.archiveFile)
	dependsOn(shadowJar)
	archiveClassifier.set "forge"

	from(rootProject.file("LICENSE")) {
		rename { "${it}_${rootProject.archivesBaseName}"}
	}
}

jar {
	archiveClassifier.set "dev"
}

sourcesJar {
	def commonSources = project(":common").sourcesJar
	dependsOn commonSources
	from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
	withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
		skip()
	}
}

// configure the maven publication
publishing {
	publications {
		mavenForge(MavenPublication) {
			artifactId = rootProject.archives_base_name + "-forge"
			from components.java
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
	}
}

unifiedPublishing {
	project {
		displayName.set "[Forge $project.supported_version] v$project.version"
		releaseType.set "release"
		gameVersions.set(["1.18", "1.18.1", "1.18.2"])
		gameLoaders.set(["forge"])

		relations {
			depends {
				curseforge.set "cloth-config"
				modrinth.set "cloth-config"
			}
		}

		System.getenv("changelog")?.with { String changelogg ->
			changelog.set changelogg
		}

		mainPublication remapJar

		System.getenv("curse_api_key")?.with { String key ->
			curseforge {
				token.set key
				id.set project.curseforge_id
				gameVersions.addAll "Java 17"
			}
		}

		System.getenv("modrinth_key")?.with { String key ->
			modrinth {
				token.set key
				id.set project.modrinth_id
				version = "$project.version+$project.name"
			}
		}
	}
}

rootProject.tasks.getByName("modPublish").dependsOn tasks.getByName("publishUnified")
