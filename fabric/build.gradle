plugins {
	id "me.shedaniel.unified-publishing"
}

architectury {
	platformSetupLoomIde()
	fabric()
}

configurations {
	common
	shadowCommon
	compileOnly.extendsFrom common
	runtimeClasspath.extendsFrom common
	developmentFabric.extendsFrom common
}

repositories {
	maven { url "https://maven.terraformersmc.com/" }
}

dependencies {
	modImplementation "net.fabricmc:fabric-loader:${rootProject.fabric_loader_version}"

	modImplementation "com.terraformersmc:modmenu:${rootProject.modmenu_version}"
	modApi("me.shedaniel.cloth:cloth-config-fabric:${rootProject.cloth_version}") {
		exclude(group: "net.fabricmc.fabric-api")
	}

	common(project(path: ":common", configuration: "namedElements")) { transitive false }
	shadowCommon(project(path: ":common", configuration: "transformProductionFabric")) { transitive false }
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

shadowJar {
	configurations = [project.configurations.shadowCommon]
	archiveClassifier.set "dev-shadow"
}

remapJar {
	input.set(shadowJar.archiveFile)
	dependsOn(shadowJar)
	archiveClassifier.set "fabric"

	from(rootProject.file("LICENSE")) {
		rename { "${it}_${rootProject.archivesBaseName}"}
	}
}

jar {
	archiveClassifier.set "dev"
}

sourcesJar {
	def commonSources = project(":common").sourcesJar
	dependsOn commonSources
	from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
	withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
		skip()
	}
}

// configure the maven publication
publishing {
	publications {
		mavenFabric(MavenPublication) {
			artifactId = rootProject.archives_base_name + "-fabric"
			from components.java
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
	}
}

unifiedPublishing {
	project {
		displayName.set "[Fabric $project.supported_version] v$project.version"
		releaseType.set "release"
		gameVersions.set(["1.19.1", "1.19.2", "1.19.3", "1.19.4"])
		gameLoaders.set(["fabric"])

		relations {
			depends {
				curseforge.set "cloth-config"
				modrinth.set "cloth-config"
			}
			optional {
				curseforge.set "modmenu"
				modrinth.set "modmenu"
			}
		}

		System.getenv("changelog")?.with { String changelogg ->
			changelog.set changelogg
		}

		mainPublication remapJar

		System.getenv("curse_api_key")?.with { String key ->
			curseforge {
				token.set key
				id.set project.curseforge_id
				gameVersions.addAll "Java 17"
			}
		}

		System.getenv("modrinth_key")?.with { String key ->
			modrinth {
				token.set key
				id.set project.modrinth_id
				version = "$project.version+$project.name"
			}
		}
	}
}

rootProject.tasks.getByName("modPublish").dependsOn tasks.getByName("publishUnified")
